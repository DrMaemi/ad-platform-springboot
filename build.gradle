plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'

	/*
		Asciidoctor 플러그인 적용
		gradle 7.0 이상부터는 jvm 사용
	 */
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.ssg'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	asciidoctorExtensions // dependencies 에서 적용한 것 추가
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'com.h2database:h2'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.jetbrains:annotations:20.1.0'
	annotationProcessor('org.projectlombok:lombok')
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	/*
		build/generated-snippets 에 생긴 .adoc 조각들을 프로젝트 내의 .adoc 파일에서 읽어들일 수 있도록 연동
		이 덕분에 .adoc 파일에서 operation 같은 매크로를 사용하여 스니펫 조각들을 연동할 수 있음
		최종적으로 .adoc 파일을 HTML로 만들어 export
	 */
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	/*
		restdocs-mockmvc의 testCompile 구성 -> MockMvc를 사용해서 snippets 조각들 생성
		MockMvc 대신 WebTestClient을 사용하려면 spring-restdocs-webtestclient 추가
		MockMvc 대신 REST Assured를 사용하려면 spring-restdocs-restassured 를 추가
	 */
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
	// 위에서 작성한 snippetsDir 디렉토리를 test의 output으로 구성하는 설정 -> 스니펫 조각들이 build/generated-snippets로 출력
	outputs.dir snippetsDir

	useJUnitPlatform()
}
asciidoctor { // asciidoctor 작업 구성
	dependsOn test // 문서 생성 전 테스트가 실행되도록 test에 종속 설정
	configurations 'asciidoctorExtensions' // 위에서 작성한 configuration 적용
	inputs.dir snippetsDir // snippetsDir 를 입력으로 구성

	/*
		source가 없으면 .adoc파일을 전부 html로 만들어버림
		source 지정시 특정 adoc만 HTML로 만든다.
	 */
	sources{
		include("/**/api.adoc","/**/common/*.adoc")
	}

	/*
		특정 .adoc에 다른 adoc 파일을 가져와서(include) 사용하고 싶을 경우 경로를 baseDir로 맞춰주는 설정입니다.
		개별 adoc으로 운영한다면 필요 없는 옵션
	 */
	baseDirFollowsSourceFile()
}

// static/docs 폴더 비우기
//asciidoctor.doFirst {
//	delete file('src/main/resources/static/docs')
//}

// asccidoctor 작업 이후 생성된 HTML 파일을 static/docs 로 copy
task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/templates/docs")
}

// build 의 의존작업 명시
build {
	dependsOn copyDocument
}

/*
	[참고사항]
	공식 문서에서는 위의 ascidoctor.doFirst부터 아래 내용은 없고 이와 같은 내용만 있습니다.
	이렇게 하면 jar로 만들어 질때 옮겨지는 것으로 IDE로 돌릴 때는 build 폴더에서만 확인이 가능합니다.
	위 방법을 사용하면 IDE에서도 static으로 옮겨진 것을 확인할 수 있습니다.
	위에 방법을 사용하든 아래 방법을 사용하든 편한 선택지를 사용하시면 됩니다.
 */
bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}
